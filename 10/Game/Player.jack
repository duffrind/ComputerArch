/**
 * Creates a player. A player has x and y coordinate location and a size.
 * There are methods for changing the size, drawing, erasing, moving,
 * and returning the size and coordinates.
 */

class Player {

    // Location on the screen
    field int x, y;

    // The size of the player
    field int size;

    /** Constructs a new player with a given location and size. */
    constructor Player new(int Ax, int Ay, int Asize) {
        let x = Ax;
        let y = Ay;
        let size = Asize;

        do draw();

        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Draws the player on the screen. */
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;
    }

    /** Erases the player from the screen. */
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;
    }

    /** Increments the size by 5 pixels. */
    method void incSize() {
        if (((y + size) < 254) & ((x + size) < 510)) {
            do erase();
            let size = size + 5;
            do draw();
        }
        return;
    }

    /** Decrements the size by 5 pixels. */
    method void decSize() {
        if (size > 2) {
            do erase();
            let size = size - 5;
            do draw();
        }
        return;
	}

    /** Moves up by 2 pixels. */
    method void moveUp() {
        if (y > 1) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
            let y = y - 2;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + size, y + 1);
        }
        return;
    }

    /** Moves down by 2 pixel. */
    method void moveDown() {
        if ((y + size) < 253) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + size, y + 1);
            let y = y + 2;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
        }
        return;
    }

    /** Returns the current height of the player */
    method int getY() {
        return y;
    }

    /** Returns the current width of the player */
    method int getX() {
        return x;
    }

    /** Returns the current size of the player */
    method int getSize() {
        return size;
    }
}
